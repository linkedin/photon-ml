// TODO: codecoverage plugin apply plugin: 'li-jacoco-product'
// TODO: checkstyle plugin apply plugin: 'li-checkstyle-product'

buildscript {
  repositories {
    mavenCentral()
    jcenter()
  }

  // TODO: is this the right way to incluse gradle plugins?
  repositories {
    repositories {
      // For license plugin.
      maven {
        url 'http://dl.bintray.com/content/netflixoss/external-gradle-plugins/'
      }
    }
  }

}

allprojects {
  apply plugin: 'idea'
  repositories {
    mavenCentral()

    // TODO: Linkedin internal repo, should be cleaned out
    ['SNA', 'DDS', 'ext-restricted'].each { repositoryName ->
      ivy {
        url "http://artifactory.corp.linkedin.com:8081/artifactory/${repositoryName}"

        layout 'pattern', {
          m2compatible = true

          artifact '[organisation]/[module]/[revision]/[module]-[revision](.[ext])'
          ivy '[organisation]/[module]/[revision]/[module]-[revision].ivy'
        }
      }
    }
  }
}

subprojects {
  // Put the build dir into the rootProject
  buildDir = "../build/$name"

  // scala cross build script
  apply from: "${rootDir}/build-scripts/scala.gradle"

  scala {
    targetVersions '2.10.4', '2.11.7'
  }

  configurations.all {
    // Brought in by azkaban2, but not needed
    exclude group: 'commons-dbutils'
    exclude group: 'commons-fileupload'
    exclude group: 'commons-pool'
    exclude group: 'com.h2database'
    exclude group: 'mysql'
    exclude group: 'com.google.collections', module: 'google-collections'
    exclude group: 'kafka', module: 'kafka'
  }

  plugins.withType(JavaPlugin) {
    tasks.withType(Test) {
      useTestNG()

      options {
        parallel = 'methods'
        threadCount = 4
      }

      afterSuite { desc, result ->
        if (!desc.parent) {
          println ":${project.name} -- Executed ${result.testCount} tests: ${result.successfulTestCount} succeeded, ${result.failedTestCount} failed, ${result.skippedTestCount} skipped"
        }
      }

      // Forward standard out from child JVMs to the console
      testLogging {
        showStackTraces = true
        showStandardStreams = true
        showExceptions = true
        showCauses = true
        displayGranularity = maxGranularity
        exceptionFormat = 'full'
      }

      outputs.upToDateWhen { false }

      systemProperty "log4j.configuration", "file:${project.rootDir}/log4j.properties"
    }

    dependencies {
      testCompile 'org.testng:testng:6.9.9'
    }

    sourceCompatibility = 1.8
  }

  tasks.withType(ScalaCompile) {
    scalaCompileOptions.fork = true

    scalaCompileOptions.additionalParameters = [
      "-feature",
      "-deprecation",
      "-verbose",  // Output what the compiler is doing
      "-optimize",
      "-explaintypes",  // Explain type errors in more detail.
      "-g:vars",
      "-language:reflectiveCalls", // Used for config structural typing
      "-language:postfixOps"
    ]

    scalaCompileOptions.useAnt = false

    configure(scalaCompileOptions.forkOptions) {
      memoryMaximumSize = '1g'
    }
    configurations.zinc.transitive = true
  }
}